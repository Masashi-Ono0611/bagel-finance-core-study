#include "imports/stdlib.fc";
#include "jetton/params.fc";
#include "jetton/op-codes.fc";
#include "jetton/discovery-params.fc";
#include "jetton/jetton-utils.fc";
#include "vault/codes.fc";
#include "vault/storage.fc";
#include "vault/utils.fc";

const int GAS_PER_SWAP = 100000000;
const int GAS_PER_MINT_SEND = 50000000;
const int MIN_EXCHANGE_AMOUNT = 1000000000;
const int MAX_COINS = 1329227995784915872903807060280344575;
const int INDEX_UNIT = 1000000000;

() recv_internal(int balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    slice sender_address = cs~load_msg_addr();
    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    load_storage();

    if (op == op::deposit()) {
        ;; check total TON amount
        int gas_per_basket = GAS_PER_SWAP + GAS_PER_MINT_SEND;
        int gas = gas_per_basket * storage::num_baskets + gas_deposit(storage::num_baskets);
        int exchange_ton = msg_value - gas;
        throw_unless(er::min_exchange_amount(), exchange_ton >= MIN_EXCHANGE_AMOUNT);
        ;; check each TON amount
        int total_swap_amount = 0;
        cell dict_swap = new_dict();
        int i = 0;
        while (i < storage::num_baskets) {
            throw_unless(er::invalid_deposit_body(), ~ in_msg_body.slice_empty?()); ;; throw if there is no TON amount
            int swap_amount = in_msg_body~load_coins();
            total_swap_amount += swap_amount;
            throw_unless(er::invalid_ton_amount(), total_swap_amount <= exchange_ton);
            dict_swap~udict_set_builder(8, i, begin_cell().store_coins(swap_amount));
            i += 1;
        }
        
        ;; StonFiの場合はMint量を読み込むため、ここではメッセージが空かどうかをチェックしない
        ;; Mint量を読み込む前にメッセージの内容を保存
        slice saved_msg_body = in_msg_body;
        ;; swap and send to vault each baskets
        int i = 0;
        while (i < storage::num_baskets) {
            (slice slice_swap, int found_swap) = dict_swap.udict_get?(8, i);
            throw_unless(er::unexpected(), found_swap);
            int swap_amount = slice_swap~load_coins();
            (cell cell_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
            throw_unless(er::unexpected(), found_baskets);
            slice slice_baskets = cell_baskets.begin_parse();
            slice_baskets~load_coins(); ;; 重み
            slice jetton_wallet_address = slice_baskets~load_msg_addr(); ;; Jettonウォレットアドレス
            cell dex_data = slice_baskets~load_ref();
            slice slice_dex = dex_data.begin_parse();
            
            ;; ========== DEXタイプの処理 ==========
            ;; デフォルトはDeDustタイプ
            int dex_type = DEX_TYPE_DEDUST();
            
            ;; バスケットにタイプ情報があれば読み込む
            if (~ slice_dex.slice_empty?()) {
                dex_type = slice_dex~load_uint(2); ;; 2ビットでDEXタイプを表現
            }
            
            ;; DEX固有のアドレス変数
            slice dex_address = null();
            slice stonfi_jetton_wallet_on_router = null(); ;; StonFi用のルーター上のJettonウォレットアドレス
            
            if (dex_type == DEX_TYPE_DEDUST()) {
                ;; DeDustの場合
                dex_address = slice_dex~load_msg_addr(); ;; プールアドレス
                slice_dex~load_msg_addr(); ;; Jettonバウルトアドレスをスキップ
            } else {
                if (dex_type == DEX_TYPE_STONFI()) {
                    ;; StonFiの場合
                    slice router_address = slice_dex~load_msg_addr(); ;; ルーターアドレス
                    slice proxy_ton_address = slice_dex~load_msg_addr(); ;; プロキシTONアドレス
                    stonfi_jetton_wallet_on_router = slice_dex~load_msg_addr(); ;; ルーター上のJettonウォレットアドレス
                    
                    ;; プロキシTONアドレスを使用
                    dex_address = proxy_ton_address;
                } else {
                    ;; 不明なDEXタイプの場合
                    dex_address = slice_dex~load_msg_addr();
                }
            }
            
            ;; ========== DEXスワップ処理 ==========
            ;; DEXタイプに応じたスワップ処理を実行
            
            ;; デフォルトのルーター上ウォレットアドレス（空アドレス）
            slice router_jetton_wallet = addr_none();
            
            ;; StonFiの場合はルーター上のJettonウォレットアドレスを使用
            if (dex_type == DEX_TYPE_STONFI()) {
                router_jetton_wallet = stonfi_jetton_wallet_on_router;
            }
            
            ;; スワップ処理の実行
            swap_dex_ton(
                query_id,
                swap_amount,
                gas_per_basket,
                dex_address,
                sender_address,
                dex_type,
                jetton_wallet_address,
                router_jetton_wallet
            );
            i += 1;
        }
        ;; ========== StonFiバスケットの処理 ==========
        ;; StonFiバスケットの数をカウント
        int stonfi_basket_count = 0;
        i = 0;
        
        while (i < storage::num_baskets) {
            (cell data_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
            throw_unless(er::invalid_vault_data(), found_baskets);
            
            slice slice_baskets = data_baskets.begin_parse();
            slice_baskets~load_coins(); ;; 重み
            slice_baskets~load_msg_addr(); ;; Jettonウォレットアドレス
            cell dex_data = slice_baskets~load_ref();
            slice slice_dex = dex_data.begin_parse();
            
            ;; DEXタイプの確認
            int dex_type = DEX_TYPE_DEDUST(); ;; デフォルトはDeDust
            
            if (~ slice_dex.slice_empty?()) {
                dex_type = slice_dex~load_uint(2); ;; DEXタイプを読み込む
            }
            
            ;; StonFiバスケットのカウント
            if (dex_type == DEX_TYPE_STONFI()) {
                stonfi_basket_count += 1;
            }
            
            i += 1;
        }
        
        ;; StonFiバスケットがある場合の処理
        if (stonfi_basket_count > 0) {
            ;; ユーザー指定のMint量を取得
            int requested_mint_amount = 0;
            
            if (~ saved_msg_body.slice_empty?()) {
                ;; メッセージからMint量を読み込む
                requested_mint_amount = saved_msg_body~load_coins();
                
                ;; 余分なデータがないことを確認
                throw_unless(er::invalid_deposit_body(), saved_msg_body.slice_empty?());
            } else {
                ;; デフォルトのMint量を使用
                requested_mint_amount = INDEX_UNIT;
            }
            
            ;; StonFiクエリ情報を登録
            storage::dict_stonfi_queries~udict_set_builder(
                64,
                query_id,
                begin_cell()
                    .store_slice(sender_address)     ;; ユーザーアドレス
                    .store_uint(stonfi_basket_count, 8)  ;; 期待するExcessメッセージ数
                    .store_uint(0, 8)               ;; 受信カウンター（初期値0）
                    .store_coins(requested_mint_amount)  ;; 要求されたMint量
            );
            
            ;; ストレージを保存
            save_storage();
        }
        
        ;; Do not send excess gas here to allow it to naturally flow to the next step in the transaction chain
        ;; The excess gas (msg_value - total_swap_amount - gas) will be automatically carried to the next operation
        return ();
    }

    if (op == op::transfer_notification()) {
        ;; check input TON amount
        throw_unless(
            er::not_enough_gas(),
            msg_value >= GAS_PER_MINT_SEND + gas_transfer_notification_complete(storage::num_baskets)
        );
        ;; check if the sender address matches any basket jetton wallet addresses stored in the dictionary
        int basket_idx = -1;
        int i = 0;
        while (i < storage::num_baskets) {
            (cell data_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
            throw_unless(er::invalid_vault_data(), found_baskets);
            slice slice_baskets = data_baskets.begin_parse();
            slice_baskets~load_coins(); ;; weight
            slice jetton_wallet_address = slice_baskets~load_msg_addr();
            if (equal_slices(jetton_wallet_address, sender_address)) {
                basket_idx = i;
            }
            i += 1;
        }
        throw_unless(er::non_basket_token(), basket_idx >= 0);

        int amount_jetton = in_msg_body~load_coins();
        slice user_address = in_msg_body~load_msg_addr(); ;; address which sent jetton (should be user or DeDust vault address)
        cell custom_payload = in_msg_body~load_maybe_ref();
        ;; get user address from payload when DeDust vault sent jetton
        if (~ cell_null?(custom_payload)) {
            slice payload_slice = custom_payload.begin_parse();
            user_address = payload_slice~load_msg_addr();
        }

        ;; At this point, we know we're in a Jetton Notify transaction
        ;; ミント後に直接リファンドが実行されるため、特別な追跡操作は不要

        cell dict_balances = new_dict();
        ;; get tmp balances from storage 
        (slice slice_waitings, int found_waitings) = storage::dict_waitings.udict_get?(
            256,
            slice_hash(user_address)
        );
        if (found_waitings) {
            dict_balances = slice_waitings~load_dict();
        }

        ;; save the amount of received jetton (key: basket idx, value: amount of jetton)
        (cell cell_balances, int found_balances) = dict_balances.udict_get_ref?(
            8,
            basket_idx
        );
        int current_balance = 0;
        if (found_balances) {
            slice slice_balances = cell_balances.begin_parse();
            current_balance += slice_balances~load_coins();
        }
        dict_balances~udict_set_ref(
            8,
            basket_idx,
            begin_cell().store_coins(current_balance + amount_jetton).end_cell()
        );

        ;; check all basket tokens
        ;; calculate mint amount if there are all basket tokens
        ;; end if there are lacking basket tokens
        int mint_amount = MAX_COINS;
        int mint_idx = -1;
        int i = 0;
        while (i < storage::num_baskets) {
            (cell cell_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
            throw_unless(er::invalid_vault_data(), found_baskets);
            slice slice_baskets = cell_baskets.begin_parse();
            int weight = slice_baskets~load_coins();
            (cell cell_balances, int found_balances) = dict_balances.udict_get_ref?(8, i);
            if (found_balances) {
                ;; if jetton exist in user tmp balances,
                ;; calculate mint amount by received_balance * jetton_weight / index_unit (1,000,000,000)
                slice slice_balances = cell_balances.begin_parse();
                int balance = slice_balances~load_coins();
                int tmp_mint_amount = muldiv(balance, INDEX_UNIT, weight);
                if (tmp_mint_amount < mint_amount) {
                    mint_amount = tmp_mint_amount;
                    mint_idx = i;
                }
            } else {
                ;; if any jetton doesn't exist in tmp balances,
                ;; end after saving current user tmp balances to waiting list
                storage::dict_waitings~udict_set_builder(
                    256,
                    slice_hash(user_address),
                    begin_cell().store_dict(dict_balances)
                );
                save_storage();              
                ;;累積ガスを保存
                int excess_gas = msg_value - gas_transfer_notification();
                if (excess_gas > 0) {
                    accumulate_excess_gas(excess_gas);
                    save_storage();
                }
                return ();
            }
            i += 1;
        }

        ;; if mint_amount > 0
        if (mint_amount > 0) {
            mint_index(
                query_id,
                user_address,
                storage::jetton_wallet_code,
                GAS_PER_MINT_SEND,
                mint_amount
            );
            storage::total_supply += mint_amount;
            int i = 0;
            while (i < storage::num_baskets) {
                if (i != mint_idx) {
                    (cell cell_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
                    throw_unless(er::invalid_vault_data(), found_baskets);
                    slice slice_baskets = cell_baskets.begin_parse();
                    int weight = slice_baskets~load_coins();
                    slice jetton_wallet_address = slice_baskets~load_msg_addr();
                    (cell cell_balances, int found_balances) = dict_balances.udict_get_ref?(8, i);
                    slice slice_balances = cell_balances.begin_parse();
                    int balance = slice_balances~load_coins();
                    int store_amount = muldivc(mint_amount, weight, INDEX_UNIT);
                    int extra_amount = balance - store_amount;
                    ;;大野メモ 今後の修正点 転送するJettonがTON建で0.01TON以下なら転送をしない方が有利。この実装は一旦スキップ
                    if (extra_amount > 0) {
                        send_jetton(
                            query_id,
                            jetton_wallet_address,
                            GAS_PER_MINT_SEND,
                            extra_amount,
                            user_address
                        );
                    }
                }
                i += 1;
            }
            storage::dict_waitings~udict_delete?(256, slice_hash(user_address));
        }

        save_storage();
        ;;累積ガスを保存
        int excess_gas = msg_value - gas_transfer_notification(); ;;;大野メモ 今後の修正点 ジェットン転送の想定ガス代(約0.01TON) * (バスケットの数-1) を追加で引きたいが、一旦スキップ
        if (excess_gas > 0) {
            accumulate_excess_gas(excess_gas);
            save_storage();
        }
        ;; ミント処理完了後、累積したガスを送信
        send_accumulated_gas(query_id, user_address);
        return ();
    }

    if (op == op::excesses()) {
        ;; クエリーに対応するユーザー情報を取得
        (slice user_data, int found) = storage::dict_stonfi_queries.udict_get?(64, query_id);
        
        if (found) {
            ;; ==========================================
            ;; StonFiの場合の処理
            ;; ==========================================
            
            ;; ユーザー情報を取得
            slice user_address = user_data~load_msg_addr();
            int expected_count = user_data~load_uint(8);
            int received_count = user_data~load_uint(8);
            int requested_mint_amount = user_data~load_coins();
            
            ;; 受信カウンターを増やす
            received_count += 1;
            
            ;; 更新された受信カウンターを保存
            storage::dict_stonfi_queries~udict_set_builder(
                64,
                query_id,
                begin_cell()
                    .store_slice(user_address)
                    .store_uint(expected_count, 8)
                    .store_uint(received_count, 8)
                    .store_coins(requested_mint_amount)
            );
            save_storage();
            
            ;; すべてのExcessメッセージを受信したか確認
            if (received_count >= expected_count) {
                ;; 最終バスケットの場合、最終Excess処理のガスを差し引く
                int excess_gas = msg_value - (gas_excess_processing() + gas_dict_update() + gas_mint_processing());
                
                ;; ミント処理を実行
                mint_index(
                    query_id,
                    user_address,
                    storage::jetton_wallet_code,
                    GAS_PER_MINT_SEND,
                    requested_mint_amount
                );
                
                storage::total_supply += requested_mint_amount;
                
                ;; 処理完了後、エントリを削除
                storage::dict_stonfi_queries~udict_delete?(64, query_id);
                save_storage();
                
                ;; ミント処理完了後、残りのガスを累積
                if (excess_gas > 0) {
                    accumulate_excess_gas(excess_gas);
                    save_storage();
                }
                
                ;; 累積したガスを送信
                send_accumulated_gas(query_id, user_address);
            } else {
                ;; 中間バスケットの場合、中間Excess処理のガスを差し引く
                int excess_gas = msg_value - (gas_excess_processing() + gas_dict_update());
                
                ;; すべてのExcessメッセージを受信していない場合は、ガスを累積
                if (excess_gas > 0) {
                    accumulate_excess_gas(excess_gas);
                    save_storage();
                }
            }
        } else {
            ;; ==========================================
            ;; DeDustの場合の処理
            ;; ==========================================
            
            ;; DeDustの場合のガス計算
            int excess_gas = msg_value - gas_excesses();
            
            ;; 単にガスを累積するだけ
            if (excess_gas > 0) {
                accumulate_excess_gas(excess_gas);
                save_storage();
            }
        }
        
        return ();
    }
    
    
    if (op == op::excesses_without_accumulated()) {
        ;; 外部からのexcesses_without_accumulated呼び出し時にガスをそのまま返還
        ;; 送信元に返還するだけなので、dict_excessesを参照する必要はない
        send_excesses(query_id, sender_address, msg_value - gas_excesses(), 2);
        return ();
    }

    if (op == op::burn_notification()) {
        int jetton_amount = in_msg_body~load_coins();
        slice from_address = in_msg_body~load_msg_addr();
        throw_unless(
            er::unauthorized_burn(),
            equal_slices(calculate_user_jetton_wallet_address(from_address, my_address(), storage::jetton_wallet_code), sender_address)
        );
        storage::total_supply -= jetton_amount;
        save_storage();

        ;; check input TON amount
        int gas_per_basket = GAS_PER_SWAP + GAS_PER_MINT_SEND;
        int gas = gas_per_basket * storage::num_baskets;
        throw_unless(er::not_enough_gas(), msg_value >= gas + gas_burn_notification(storage::num_baskets));
        int i = 0;
        while (i < storage::num_baskets) {
            (cell cell_baskets, int found_baskets) = storage::dict_baskets.udict_get_ref?(8, i);
            throw_unless(er::invalid_vault_data(), found_baskets);
            slice slice_baskets = cell_baskets.begin_parse();
            int weight = slice_baskets~load_coins();
            slice jetton_wallet_address = slice_baskets~load_msg_addr();
            cell dex_data = slice_baskets~load_ref();
            slice slice_dex = dex_data.begin_parse();
            ;; DEXタイプを読み込む（既存のバスケットにはタイプがないため、デフォルトでDeDustとして処理）
            int dex_type = DEX_TYPE_DEDUST();
            ;; バスケットにタイプ情報があれば読み込む
            if (~ slice_dex.slice_empty?()) {
                ;; 先頭バイトがタイプ情報であるか確認
                dex_type = slice_dex~load_uint(2); ;; 2ビットで最大4種類のDEXをサポート
            }
            
            ;; DEXタイプに応じて適切なアドレスを取得
            slice dex_pool_address = null();
            slice dex_vault_address = null();
            
            if (dex_type == DEX_TYPE_DEDUST()) {
                ;; DeDustの場合はプールアドレスとVaultアドレスを使用
                dex_pool_address = slice_dex~load_msg_addr(); ;; dex_pool_address
                dex_vault_address = slice_dex~load_msg_addr(); ;; dex_vault_address
            } else {
                if (dex_type == DEX_TYPE_STONFI()) {
                    ;; StonFiの場合はルーターアドレスとプロキシTONアドレスを取得
                    slice router_address = slice_dex~load_msg_addr(); ;; dex_router_address (ルーターアドレス)
                    slice proxy_ton_address = slice_dex~load_msg_addr(); ;; dex_proxy_ton_address (プロキシTONアドレス)
                    
                    ;; ルーターアドレスをdex_pool_addressとして設定
                    dex_pool_address = router_address;
                    ;; プロキシTONアドレスをdex_vault_addressとして設定
                    dex_vault_address = proxy_ton_address;
                } else {
                    ;; 不明なDEXタイプの場合はデフォルトの動作
                    dex_pool_address = slice_dex~load_msg_addr();
                    dex_vault_address = slice_dex~load_msg_addr();
                }
            }
            int swap_amount = muldiv(jetton_amount, weight, INDEX_UNIT);
            swap_dex_jetton(
                query_id,
                jetton_wallet_address,
                gas_per_basket,
                swap_amount,
                dex_vault_address,
                GAS_PER_SWAP,
                dex_pool_address,
                from_address,
                dex_type
            );
            i += 1;
        }

        ;; We don't need to read response_address or send excesses explicitly
        ;; Excess gas will be automatically returned to the original sender at the end of the transaction chain
        ;; Skip reading the response_address from the message body
        in_msg_body~load_msg_addr();
        return ();
    }

    if (op == op::change_vault_data()) {
        throw_unless(er::not_admin(), equal_slices(sender_address, storage::admin_address));
        storage::stopped = in_msg_body~load_bool();
        storage::num_baskets = in_msg_body~load_uint(8);
        storage::dex_ton_vault_address = in_msg_body~load_msg_addr(); ;; DEX TON vault address
        storage::dict_baskets = in_msg_body~load_dict();
        save_storage();
        return ();
    }

    if (op == op::send_admin_message()) {
        throw_unless(er::not_admin(), equal_slices(sender_address, storage::admin_address));
        cell msg_cell = in_msg_body~load_ref();
        int send_mode = in_msg_body~load_uint(8);
        send_raw_message(msg_cell, send_mode);
        return ();
    }

    if (op == op::change_code_and_data()) {
        throw_unless(er::not_admin(), equal_slices(sender_address, storage::admin_address));
        cell code_cell = in_msg_body~load_ref();
        cell data_cell = in_msg_body~load_ref();
        set_code(code_cell);
        set_data(data_cell);
        return ();
    }

    if (op == op::change_admin()) {
        throw_unless(er::not_admin(), equal_slices(sender_address, storage::admin_address));
        storage::admin_address = in_msg_body~load_msg_addr();
        save_storage();
        return ();
    }

    if (op == op::change_content()) {
        throw_unless(er::not_admin(), equal_slices(sender_address, storage::admin_address));
        storage::content = in_msg_body~load_ref();
        save_storage();
        return ();
    }

    if (op == op::mint()) {
        throw_unless(73, equal_slices(sender_address, storage::admin_address));
        slice to_address = in_msg_body~load_msg_addr();
        int amount = in_msg_body~load_coins();
        cell master_msg = in_msg_body~load_ref();
        slice master_msg_cs = master_msg.begin_parse();
        master_msg_cs~skip_bits(32 + 64); ;; op + query_id
        int jetton_amount = master_msg_cs~load_coins();
        mint_tokens(to_address, storage::jetton_wallet_code, amount, master_msg);
        storage::total_supply += jetton_amount;
        save_storage();
        return ();
    }

    if (op == op::provide_wallet_address()) {
        throw_unless(er::discovery_fee_not_matched(), msg_value > fwd_fee + provide_address_gas_consumption());

        slice owner_address = in_msg_body~load_msg_addr();
        int include_address? = in_msg_body~load_uint(1);

        cell included_address = include_address?
            ? begin_cell().store_slice(owner_address).end_cell()
            : null();

        var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(sender_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::take_wallet_address(), 32)
            .store_uint(query_id, 64);

        if (is_resolvable?(owner_address)) {
            msg = msg.store_slice(calculate_user_jetton_wallet_address(owner_address, my_address(), storage::jetton_wallet_code));
        } else {
            msg = msg.store_uint(0, 2); ;; addr_none
        }
        send_raw_message(msg.store_maybe_ref(included_address).end_cell(), 64);
        return ();
    }

    throw(er::wrong_op());
}

(int, int, slice, cell, cell) get_jetton_data() method_id {
    load_storage();
    return (
        storage::total_supply, 
        -1, 
        storage::admin_address, 
        storage::content, 
        storage::jetton_wallet_code
    );
}

slice get_wallet_address(slice admin_address) method_id {
    load_storage();
    return calculate_user_jetton_wallet_address(
        admin_address,
        my_address(),
        storage::jetton_wallet_code
    );
}

(int, int, slice, cell, cell, int) get_vault_data() method_id {
    load_storage();
    return (
        storage::stopped,
        storage::num_baskets,
        storage::dex_ton_vault_address,
        storage::dict_baskets,
        storage::dict_waitings,
        storage::accumulated_gas
    );
}

cell get_waitings() method_id {
    load_storage();
    return storage::dict_waitings;
}

cell get_excesses() method_id {
    ;; 新しい実装では内部転送カウンターが不要になったため、空の辞書を返す
    return new_dict();
}